Imports System.Collections.Generic
Imports System.IO
Imports System.Linq
Imports System.Windows.Forms
Imports SolidWorks.Interop.sldworks
Imports SolidWorks.Interop.swconst
Imports System.Threading

Public Module svnAddInUtils
    Public Function findSvnRoot(filePath As String, Optional bTrimEnd As Boolean = True) As String
        Dim currentDir As DirectoryInfo = New FileInfo(filePath).Directory

        While currentDir IsNot Nothing
            Dim svnFolder As DirectoryInfo = New DirectoryInfo(Path.Combine(currentDir.FullName, ".svn"))

            If svnFolder.Exists Then
                findSvnRoot = currentDir.FullName
                If bTrimEnd Then findSvnRoot = findSvnRoot.TrimEnd("\"c)
                If bTrimEnd Then findSvnRoot = findSvnRoot.TrimEnd("\"c)
                Return findSvnRoot
            End If

            currentDir = currentDir.Parent
        End While

        ' .svn not found in any parent folders
        If bTrimEnd Then filePath = filePath.TrimEnd("\"c)
        If bTrimEnd Then filePath = filePath.TrimEnd("\"c)

        Return filePath
    End Function
    Public Function createBoolArray(ByRef iUbound As Integer, ByRef value As Boolean) As Boolean()
        Dim i As Integer
        Dim output(iUbound) As Boolean
        For i = 0 To iUbound
            output(i) = value
        Next
        Return output
    End Function
    Public Function catWithNewLine(stringArr() As String, Optional wrapLength As Integer = 50) As String
        Dim i As Integer
        Dim output As String = ""
        If stringArr Is Nothing Then Return ""
        For i = 0 To UBound(stringArr)
            If stringArr(i) Is Nothing Then Continue For

            ' wraplength creates a newline if the original line is too long, essentially wrapping the text to prevent too many in a single line. sendmsgtouser2 truncates if too many char in one line
            If stringArr(i).Length > wrapLength * 2 Then
                output &= vbCrLf & stringArr(i).Substring(0, wrapLength)
                output &= vbCrLf & stringArr(i).Substring(wrapLength, wrapLength)
                output &= vbCrLf & stringArr(i).Substring(wrapLength * 2)

            ElseIf stringArr(i).Length > wrapLength Then

                output &= vbCrLf & stringArr(i).Substring(0, wrapLength)
                output &= vbCrLf & stringArr(i).Substring(wrapLength)

            Else

                output &= vbCrLf & stringArr(i)
            End If

        Next
        Return output
    End Function
    Public Sub CopyToClipboard(text As String)

        Clipboard.SetText(text)

    End Sub
    Public Sub hideButton(button As ToolStripSplitButton)
        'ToolStripSplitButFolder.HideDropDown()

        button.DropDown.Close()

        'If button.Owner IsNot Nothing AndAlso button.Owner.InvokeRequired Then
        'If button.Owner IsNot Nothing Then

        '    button.Owner.BeginInvoke(New MethodInvoker(Sub()
        '                                                   button.HideDropDown()
        '                                               End Sub))
        'Else

        'End If
    End Sub

    Public Sub CloseDropDown(menuItem As ToolStripMenuItem)
        'ToolStripSplitButFolder.HideDropDown()
        'If menuItem.Owner IsNot Nothing AndAlso menuItem.Owner.InvokeRequired Then
        '    menuItem.Owner.BeginInvoke(New MethodInvoker(Sub()
        '                                                     menuItem.HideDropDown()
        '                                                 End Sub))
        'Else

        'End If
        menuItem.DropDown.Close()
    End Sub

    Public Function vLookup(lookupValue As String,
                             tableArray As String(,),
                             returnColumn As Integer) As String

        If tableArray Is Nothing Then Return Nothing

        Dim rowCount As Integer = tableArray.GetLength(0)
        Dim colCount As Integer = tableArray.GetLength(1)

        ' Ensure column indices are within bounds
        If returnColumn < 0 OrElse returnColumn >= colCount Then Return Nothing

        ' Loop through each row
        For i As Integer = 0 To rowCount - 1
            If String.Equals(tableArray(i, 0), lookupValue, StringComparison.OrdinalIgnoreCase) Then
                Return tableArray(i, returnColumn)
            End If
        Next

        ' Not found
        Return Nothing
    End Function

    Public Function findIndexContains(ByVal sLookInArr() As String, ByVal find As String) As Integer
        Dim i As Integer
        'Dim output As Integer
        For i = 0 To UBound(sLookInArr)
            'If sLookInArr(i).Contains(find) Then Return i
            If (Strings.InStr(sLookInArr(i), find, CompareMethod.Text) <> 0) Then Return i
        Next
        Return -1
    End Function
    Public Sub DeleteTreeViewAt(ByVal index As Integer, ByRef prLst As TreeView())
        Dim i As Integer

        ' Move all element back one position
        For i = index + 1 To UBound(prLst)
            prLst(i - 1) = prLst(i)
        Next

        ' Shrink the array by one, removing the last one
        ReDim Preserve prLst(UBound(prLst) - 1)
    End Sub
    Public Function GetSolidworksCustomProperty(doc As ModelDoc2, propName As String) As String
        Dim valOut As String = ""
        Dim resolvedVal As String = ""
        'Dim wasResolved As Boolean
        Dim found As Boolean

        Dim custMgr As CustomPropertyManager = doc.Extension.CustomPropertyManager("")
        found = custMgr.Get4(propName, False, valOut, resolvedVal)

        If found Then
            Return resolvedVal
        Else
            Return ""
        End If
    End Function

    Public Sub SetSolidworksCustomProperty(doc As ModelDoc2, propName As String, propValue As String)
        Dim custMgr As CustomPropertyManager = doc.Extension.CustomPropertyManager("")
        custMgr.Add3(propName, swCustomInfoType_e.swCustomInfoText, propValue, swCustomPropertyAddOption_e.swCustomPropertyReplaceValue)
    End Sub

    Public Function ensureUserHasLocks(modDocArr() As ModelDoc2, Optional bRetry As Boolean = True) As Boolean()
        ' TODO: 1. Fix functions that expect single boolean output. 2. move the ensure not nothing of each element of the array to parent functions
        Dim j As Integer = 0

        '        For j = 0 To UBound(modDocArr)
        '        Next

        Dim mySVNStatus = getFileSVNStatus(bCheckServer:=False, modDocArr, bUpdateStatusOfAllOpenModels:=False)
        'Dim modDocArr_noNothing() As ModelDoc2 = RemoveNullsFromArray(modDocArr)

        Dim userHasLock(modDocArr.Length - 1) As Boolean
        Dim modsNeedingLocks As New List(Of ModelDoc2)

        For i As Integer = 0 To modDocArr.Length - 1
            If modDocArr(i) Is Nothing Then
                userHasLock(i) = Nothing
                Continue For
            End If

            If mySVNStatus.fp(i).lock6 = "K" Then
                userHasLock(i) = True
            Else
                userHasLock(i) = False
                modsNeedingLocks.Add(modDocArr(i))
            End If
        Next

        If modsNeedingLocks.Count = 0 Then
            ' User has all the locks!
            Return userHasLock
        ElseIf bRetry Then
            'Didn't have all the locks, but First time, so try to get them. 
            ' #TODO Would be better to use runSvnByArgs instead of using tortoise
            getLocksOfDocs(modsNeedingLocks.ToArray())
            ' Check again!
            Return ensureUserHasLocks(modDocArr, bRetry:=False)
        Else
            ' don't have all the locks, and have already tried once to get them. 
            Return userHasLock
        End If
    End Function
    Public Function RemoveNullsFromArray(Of T)(inputArray() As T) As T()
        If inputArray Is Nothing Then Return New T() {}
        Return inputArray.Where(Function(x) x IsNot Nothing).ToArray()
    End Function

    Public Function checkNoLocks(modDocArr() As ModelDoc2) As Boolean
        Dim mySVNStatus = getFileSVNStatus(bCheckServer:=False, modDocArr, bUpdateStatusOfAllOpenModels:=False)
        Dim userHasLock(modDocArr.Length - 1) As Boolean

        For i As Integer = 0 To modDocArr.Length - 1
            If mySVNStatus.fp(i).lock6 = "K" Then
                Return False
            End If
        Next

        Return True

    End Function
    Public Function getMatchingDrawingForArray(modDocArr As ModelDoc2(), iSwApp As SldWorks) As ModelDoc2()
        Dim outputList As New List(Of ModelDoc2)(modDocArr)
        Dim modDocPath As String
        Dim extension As String

        For Each modDoc In modDocArr
            modDocPath = modDoc.GetPathName()
            If String.IsNullOrWhiteSpace(modDocPath) Then Continue For
            extension = Path.GetExtension(modDocPath).ToUpperInvariant()

            Dim result As ModelDoc2() = getMatchingComponentAndDrawing(modDoc, iSwApp)
            If result.Length >= 2 Then
                If (extension = ".SLDDRW") And (result(0) IsNot Nothing) Then 'if the original was a drawing and we found a prt/asy then add it
                    outputList.Add(result(0))
                ElseIf ((extension = ".SLDASM") Or (extension = ".SLDPRT")) And (result(1) IsNot Nothing) Then 'if the original was a asy/prt, and we found a drawing, then add it
                    outputList.Add(result(1))
                End If
            End If
        Next

        Return outputList.ToArray()
    End Function
    Public Function getMatchingDrawingForArrayPath(modDocArr As ModelDoc2(), Optional bTitleOnly As Boolean = False) As String()
        Dim outputList As New List(Of String)
        Dim folder As String
        Dim baseName As String
        Dim drwPath As String
        Dim modDocPath As String
        'Dim extension As String

        For Each modDoc In modDocArr
            modDocPath = modDoc.GetPathName()
            folder = Path.GetDirectoryName(modDocPath)
            baseName = Path.GetFileNameWithoutExtension(modDocPath)

            If String.IsNullOrWhiteSpace(modDocPath) Then Continue For

            If bTitleOnly Then
                outputList.Add(Path.GetFileName(modDocPath))
            Else
                outputList.Add(modDocPath)
            End If

            drwPath = Path.Combine(folder, baseName & ".SLDDRW")

            If File.Exists(drwPath) Then
                If bTitleOnly Then
                    outputList.Add(Path.GetFileName(drwPath))
                Else
                    outputList.Add(drwPath)
                End If

            End If

        Next

        Return outputList.ToArray()
    End Function

    Public Function getMatchingComponentAndDrawing(modDoc As ModelDoc2, iSwApp As SldWorks, Optional bOpenFile As Boolean = True) As ModelDoc2()
        Dim modDocPath As String = modDoc.GetPathName()
        If String.IsNullOrWhiteSpace(modDocPath) Then Return Nothing
        Dim folder As String = Path.GetDirectoryName(modDocPath)
        Dim baseName As String = Path.GetFileNameWithoutExtension(modDocPath)
        Dim extension As String = Path.GetExtension(modDocPath).ToUpperInvariant()
        Dim result(1) As ModelDoc2

        'Important: Part/Assemble is always in position 0. Drawing always in position 1. 

        ' Check if it's a Part or Assembly
        If extension = ".SLDPRT" OrElse extension = ".SLDASM" Then
            result(0) = modDoc

            ' Look for matching drawing
            Dim drwPath As String = Path.Combine(folder, baseName & ".SLDDRW")
            If File.Exists(drwPath) Then
                result(1) = CType(iSwApp.OpenDoc6(drwPath, swDocumentTypes_e.swDocDRAWING, swOpenDocOptions_e.swOpenDocOptions_Silent, "", 0, 0), ModelDoc2)
            End If

        ElseIf extension = ".SLDDRW" Then
            result(1) = modDoc

            ' Try .SLDPRT first
            Dim prtPath As String = Path.Combine(folder, baseName & ".SLDPRT")
            If File.Exists(prtPath) Then
                result(0) = CType(iSwApp.OpenDoc6(prtPath, swDocumentTypes_e.swDocPART, swOpenDocOptions_e.swOpenDocOptions_Silent, "", 0, 0), ModelDoc2)
            Else
                ' Try .SLDASM
                Dim asmPath As String = Path.Combine(folder, baseName & ".SLDASM")
                If File.Exists(asmPath) Then
                    result(0) = CType(iSwApp.OpenDoc6(asmPath, swDocumentTypes_e.swDocASSEMBLY, swOpenDocOptions_e.swOpenDocOptions_Silent, "", 0, 0), ModelDoc2)
                End If
            End If

        Else
            iSwApp.SendMsgToUser("Error. Not a part, assembly, or drawing. Exiting.")
            Return Nothing ' Not a part, assembly, or drawing
        End If
        Return result   'Important: Part/Assemble is always in position 0. Drawing always in position 1. 
    End Function
    Public Function userFilePickerFromList(modDocArr As ModelDoc2()) As ModelDoc2()
        ' Create and show the form
        Dim filterForm As New ModelDocFilterForm(modDocArr)
        If filterForm.ShowDialog() = DialogResult.OK Then
            Return filterForm.FilteredDocs
        Else
            Return Nothing ' Return original if user cancels
        End If
    End Function

    Public Class ModelDocFilterForm
        Inherits Form

        Private checkedListBox As New CheckedListBox()
        Private okButton As New Button()
        Private docList As New List(Of ModelDoc2)

        Private _filteredDocs As ModelDoc2()

        Public ReadOnly Property FilteredDocs As ModelDoc2()
            Get
                Return _filteredDocs
            End Get
        End Property

        Public Sub New(modDocArr As ModelDoc2())
            Me.Text = "Select Files"
            Me.Size = New Drawing.Size(500, 400)
            Me.StartPosition = FormStartPosition.CenterScreen

            checkedListBox.Dock = DockStyle.Fill
            checkedListBox.CheckOnClick = True

            For Each doc As ModelDoc2 In modDocArr
                Dim fileName As String = IO.Path.GetFileName(doc.GetPathName())
                checkedListBox.Items.Add(fileName, True)
                docList.Add(doc)
            Next

            okButton.Text = "OK"
            okButton.Dock = DockStyle.Bottom
            AddHandler okButton.Click, AddressOf OkButton_Click

            Me.Controls.Add(checkedListBox)
            Me.Controls.Add(okButton)
        End Sub

        Private Sub OkButton_Click(sender As Object, e As EventArgs)
            Dim selectedDocs As New List(Of ModelDoc2)

            For i As Integer = 0 To checkedListBox.Items.Count - 1
                If checkedListBox.GetItemChecked(i) Then
                    selectedDocs.Add(docList(i))
                End If
            Next

            _filteredDocs = selectedDocs.ToArray()
            Me.DialogResult = DialogResult.OK
            Me.Close()
        End Sub
    End Class
    Public Function boolFilter(ByVal sArr() As String, bFilter() As Boolean) As String()
        Dim lsReturn As New List(Of String)

        If UBound(sArr) <> UBound(bFilter) Then Return Nothing

        For i As Integer = 0 To UBound(sArr)
            If bFilter(i) Then lsReturn.Add(sArr(i))
        Next
        Return lsReturn.ToArray
    End Function
    Public Function getTitleClean(modDoc As ModelDoc) As String
        Return Path.GetFileName(modDoc.GetPathName())
    End Function
End Module
